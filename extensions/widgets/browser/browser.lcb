/*
Copyright (C) 2015 Runtime Revolution Ltd.

This file is part of LiveCode.

LiveCode is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License v3 as published by the Free
Software Foundation.

LiveCode is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
for more details.

You should have received a copy of the GNU General Public License
along with LiveCode.  If not see <http://www.gnu.org/licenses/>.  */

/*
Module: com.livecode.extension.livecode.browser

Type: widget

Description: This widget is a web browser.
*/

-- declaring extension as widget, followed by identifier
widget com.livecode.extensions.livecode.browser
--

-- dependancy declarations
use com.livecode.foreign
use com.livecode.widget
use com.livecode.engine
--

-- adding metadata to ensure the extension displays correctly in livecode
metadata title is "Browser"
metadata author is "Ian Macphail"
metadata version is "1.0.0"
--

property uri get getUrl set setUrl
metadata uri.editor is "com.livecode.pi.text"

property htmltext get getHtmlText set setHtmlText
metadata htmltext.editor is "com.livecode.pi.text"

property scrollbars get getScrollbars set setScrollbars
metadata scrollbars.editor is "com.livecode.pi.boolean"

property useragent get getUserAgent set setUserAgent
metadata useragent.editor is "com.livecode.pi.text"

property javascripthandlers get getJavaScriptHandlers set setJavaScriptHandlers
metadata javascripthandlers.editor is "com.livecode.pi.text"

--------------------------------------------------------------------------------

type MCBrowserFactoryRef is Pointer
type MCBrowserRef is Pointer

type MCBrowserListRef is Pointer

type MCBrowserProperty is CInt
type MCBrowserValueType is CInt
type MCBrowserRequestType is CInt
type MCBrowserRequestState is CInt

--

foreign handler MCBrowserLibraryInitialize() returns CBool binds to "<builtin>"
foreign handler MCBrowserLibraryFinalize() returns nothing binds to "<builtin>"

foreign handler MCBrowserFactoryGet(in pFactoryId as NativeCString, out rFactory as MCBrowserFactoryRef) returns CBool binds to "<builtin>"
foreign handler MCBrowserFactoryCreateBrowser(in pFactory as MCBrowserFactoryRef, out rBrowser as MCBrowserRef) returns CBool binds to "<builtin>"

foreign handler MCBrowserRetain(in pBrowser as MCBrowserRef) returns MCBrowserRef binds to "<builtin>"
foreign handler MCBrowserRelease(in pBrowser as MCBrowserRef) returns nothing binds to "<builtin>"

foreign handler MCBrowserGetNativeLayer(in pBrowser as MCBrowserRef) returns Pointer binds to "<builtin>"

foreign handler MCBrowserGetBoolProperty(in pBrowser as MCBrowserRef, in pProperty as MCBrowserProperty, out rValue as CBool) returns CBool binds to "<builtin>"
foreign handler MCBrowserSetBoolProperty(in pBrowser as MCBrowserRef, in pProperty as MCBrowserProperty, in pValue as CBool) returns CBool binds to "<builtin>"

foreign handler MCBrowserGetStringProperty(in pBrowser as MCBrowserRef, in pProperty as MCBrowserProperty, out rValue as NativeCString) returns CBool binds to "<builtin>"
foreign handler MCBrowserSetStringProperty(in pBrowser as MCBrowserRef, in pProperty as MCBrowserProperty, in pValue as NativeCString) returns CBool binds to "<builtin>"

foreign handler MCBrowserGoBack(in pBrowser as MCBrowserRef) returns CBool binds to "<builtin>"
foreign handler MCBrowserGoForward(in pBrowser as MCBrowserRef) returns CBool binds to "<builtin>"
foreign handler MCBrowserGoToURL(in pBrowser as MCBrowserRef) returns CBool binds to "<builtin>"
foreign handler MCBrowserEvaluateJavaScript(in pBrowser as MCBrowserRef, in pScript as NativeCString, out rResult as NativeCString) returns CBool binds to "<builtin>"

----------

foreign handler MCBrowserListGetSize(in pList as MCBrowserListRef, out pSize as CUInt) returns CBool binds to "<builtin>"
foreign handler MCBrowserListGetType(in pList as MCBrowserListRef, in pIndex as CUInt, out pType as MCBrowserValueType) returns CBool binds to "<builtin>"
foreign handler MCBrowserListGetBoolean(in pList as MCBrowserListRef, in pIndex as CUInt, out pValue as CBool) returns CBool binds to "<builtin>"
foreign handler MCBrowserListGetInteger(in pList as MCBrowserListRef, in pIndex as CUInt, out pValue as CInt) returns CBool binds to "<builtin>"
foreign handler MCBrowserListGetDouble(in pList as MCBrowserListRef, in pIndex as CUInt, out pValue as CDouble) returns CBool binds to "<builtin>"
foreign handler MCBrowserListGetUTF8String(in pList as MCBrowserListRef, in pIndex as CUInt, out pValue as ZStringNative) returns CBool binds to "<builtin>"
foreign handler MCBrowserListGetList(in pList as MCBrowserListRef, in pIndex as CUInt, out pValue as MCBrowserListRef) returns CBool binds to "<builtin>"

foreign handler MCBrowserSetRequestHandler(in pBrowser as MCBrowserRef, in pCallback as Pointer, in pContext as optional Pointer) returns CBool binds to "<builtin>"
foreign handler MCBrowserSetJavaScriptHandler(in pBrowser as MCBrowserRef, in pCallback as Pointer, in pContext as optional Pointer) returns CBool binds to "<builtin>"

----------

foreign handler MCHandlerGetFunctionPtr(in pHandler, out rFuncPtr as Pointer) returns CBool binds to "<builtin>"

--------------------------------------------------------------------------------

variable mBrowser as optional MCBrowserRef
variable mBrowserType as String
variable mProperties as optional Array

variable mContentSource as String

--

variable mRequestHandler
variable mJavaScriptHandler

--------------------------------------------------------------------------------

constant kValueTypeNone is 0
constant kValueTypeBoolean is 1
constant kValueTypeInteger is 2
constant kValueTypeDouble is 3
constant kValueTypeUTF8String is 4
constant kValueTypeList is 5

--

constant kPropertyScrollbars is 0
constant kPropertyAllowNewWindows is 1
constant kPropertyEnableContextMenu is 2
constant kPropertyUrl is 3
constant kPropertyHtmlText is 4
constant kPropertyUserAgent is 5
constant kPropertyJavaScriptHandlers is 6

constant kPropertyMap is ["scrollbars", "allowNewWindows", "enableContextMenu", "url", "htmlText", "userAgent", "javaScriptHandlers"]

--

constant kRequestTypeNavigate is 0
constant kRequestTypeDocumentLoad is 1

constant kRequestTypeMap is ["navigate", "documentLoad"]

--

constant kRequestStateBegin is 0
constant kRequestStateComplete is 1
constant kRequestStateFailed is 2

constant kRequestStateMap is ["begin", "complete", "failed"]

--

constant kStringProps is ["url", "htmlText", "userAgent", "javaScriptHandlers"]
constant kBoolProps is ["scrollbars", "allowNewWindows", "enableContextMenu"]
constant kPersistentProps is ["scrollbars", "allowNewWindows", "enableContextMenu", "userAgent", "javaScriptHandlers"]

--------------------------------------------------------------------------------

public handler OnModuleLoad()
	/* UNCHECKED */
	MCBrowserLibraryInitialize()
end handler

public handler OnLoad(in pProperties as Array)
	loadProperties(pProperties)
end handler

public handler OnSave(out rProperties as Array)
	if mBrowser is not nothing then
		put saveProperties() into mProperties
	end if
	put mProperties into rProperties
end handler

----------

-- called when the widget is created
public handler OnCreate()
	OnModuleLoad()
	
	put OnBrowserRequestCallback into mRequestHandler
	put OnJavaScriptCallback into mJavaScriptHandler

	put the empty string into mBrowserType
	put the empty array into mProperties
	put the empty string into mContentSource
end handler

--

public handler OnOpen()
	variable tParent as Pointer
	// TODO - add widget code to get stack window handler
	// put my native window into tParent

	variable tFactory as MCBrowserFactoryRef
	/* UNCHECKED */
	MCBrowserFactoryGet(mBrowserType, tFactory)
	
	/* UNCHECKED */
	MCBrowserFactoryCreateBrowser(tFactory, mBrowser)
	
	variable tBrowserView as Pointer
	/* UNCHECKED */
	put MCBrowserGetNativeLayer(mBrowser) into tBrowserView
	
	// TODO - add widget code to set native layer from view
	set my native layer to tBrowserView
	
	variable tRequestCallback as Pointer
	variable tJavaScriptCallback as Pointer
	
	MCHandlerGetFunctionPtr(mRequestHandler, tRequestCallback)
	MCHandlerGetFunctionPtr(mJavaScriptHandler, tJavaScriptCallback)

	MCBrowserSetRequestHandler(mBrowser, tRequestCallback, nothing)
	MCBrowserSetJavaScriptHandler(mBrowser, tJavaScriptCallback, nothing)
	
	loadProperties(mProperties)
end handler

--

public handler OnClose()
	// Save browser properties
	put saveProperties() into mProperties
	
	set my native layer to nothing
	
	MCBrowserRelease(mBrowser)
	put nothing into mBrowser
end handler

----------

public handler OnBrowserRequestCallback(in pContext as optional Pointer, in pBrowser as MCBrowserRef, in pType as MCBrowserRequestType, in pState as MCBrowserRequestState, in pFrame as CBool, in pUrl as NativeCString, in pError as optional NativeCString) returns nothing
	variable tType as String
	put element (pType + 1) of kRequestTypeMap into tType
	
	variable tState as String
	put element (pState + 1) of kRequestStateMap into tState
	
	log "Request %@ - %@ for url %@" with [tType, tState, pUrl]
	if pState is kRequestStateFailed then
		log "Failed: %@" with [pError]
	end if
end handler

public handler OnJavaScriptCallback(in pContext as optional Pointer, in pBrowser as MCBrowserRef, in pHandler as NativeCString, in pParams as MCBrowserListRef)
	log "JS Call: %@ (%@)" with [pHandler, pParams]
	variable tList
	if browserListToList(pParams, tList) then
		log "Params: %@" with [tList]
	end if
end handler

--

private handler browserListToList(in pBrowserList as MCBrowserListRef, out rList as List) returns Boolean
	variable tList as List
	put the empty list into tList
	
	variable tCount as CUInt
	if not MCBrowserListGetSize(pBrowserList, tCount) then
		log "couldn't get size"
		return false
	end if
	
	variable i
	repeat with i from 0 up to tCount - 1
		variable tType as MCBrowserValueType
		if not MCBrowserListGetType(pBrowserList, i, tType) then
			log "couldn't get type of %@" with [i]
			return false
		end if
		
		if tType is kValueTypeBoolean then
			variable tBoolean as CBool
			if not MCBrowserListGetBoolean(pBrowserList, i, tBoolean) then
				log "couldn't get boolean %@" with [i]
				return false
			end if
			push tBoolean onto tList
		else if tType is kValueTypeInteger then
			variable tInteger as CInt
			if not MCBrowserListGetInteger(pBrowserList, i, tInteger) then
				log "couldn't get integer %@" with [i]
				return false
			end if
			push tInteger onto tList
		else if tType is kValueTypeDouble then
			variable tDouble as CDouble
			if not MCBrowserListGetDouble(pBrowserList, i, tDouble) then
				log "couldn't get double %@" with [i]
				return false
			end if
			push tDouble onto tList
		else if tType is kValueTypeUTF8String then
			variable tUTF8String as ZStringNative
			if not MCBrowserListGetUTF8String(pBrowserList, i, tUTF8String) then
				log "couldn't get string %@" with [i]
				return false
			end if
			push tUTF8String onto tList
		else if tType is kValueTypeList then
			variable tBrowserList as MCBrowserListRef
			if not MCBrowserListGetList(pBrowserList, i, tBrowserList) then
				log "couldn't get list %@" with [i]
				return false
			end if
			variable tConvertedList as List
			if not browserListToList(tBrowserList, tConvertedList) then
				log "couldn't convert list %@" with [i]
				return false
			end if
			push tConvertedList onto tList
		else
			log "unrecognised type %@" with [tType]
			return false
		end if
	end repeat
	
	put tList into rList
	
	return true
end handler

----------

private handler getProperty(in pProperty as String) returns any
	if mBrowser is nothing then
		if pProperty is among the keys of mProperties then
			return mProperties[pProperty]
		else
			return nothing
		end if
	else
		return getBrowserProperty(pProperty)
	end if
end handler

private handler setProperty(in pProperty as String, in pValue as any)
	if mBrowser is nothing then
		put pValue into mProperties[pProperty]
	else
		setBrowserProperty(pProperty, pValue)
	end if
end handler

--

public handler getUrl() returns String
	return getProperty("url")
end handler

public handler setUrl(in pUrl as String)
	put "url" into mContentSource
	setProperty("url", pUrl)
end handler

--

public handler getHtmlText() returns String
	return getProperty("htmlText")
end handler

public handler setHtmlText(in pHtmlText as String)
	put "htmltext" into mContentSource
	setProperty("htmlText", pHtmlText)
end handler

--

public handler getUserAgent() returns String
	return getProperty("userAgent")
end handler

public handler setUserAgent(in pUserAgent as String)
	setProperty("userAgent", pUserAgent)
end handler

--

public handler getJavaScriptHandlers() returns String
	return getProperty("javaScriptHandlers")
end handler

public handler setJavaScriptHandlers(in pJavaScriptHandlers as String)
	setProperty("javaScriptHandlers", pJavaScriptHandlers)
end handler

--

public handler getScrollbars() returns Boolean
	return getProperty("scrollbars")
end handler

public handler setScrollbars(in pScrollbars as Boolean)
	setProperty("scrollbars", pScrollbars)
end handler

----------

private handler lookupProperty(in pProp as String, out rEnum as Integer) returns Boolean
	variable tIndex as Integer
	put the index of pProp in kPropertyMap into tIndex
	
	if tIndex is 0 then
		return false
	end if
	
	put tIndex - 1 into rEnum
	return true
end handler

----------

private handler getBrowserProperty(in pProperty as String) returns any
	variable tProperty as Integer
	if not lookupProperty(pProperty, tProperty) then
		return nothing
	end if
	
	variable tValue as optional any
	if pProperty is in kStringProps then
		/* UNCHECKED */
		MCBrowserGetStringProperty(mBrowser, tProperty, tValue)
	else if pProperty is in kBoolProps then
		/* UNCHECKED */
		MCBrowserGetBoolProperty(mBrowser, tProperty, tValue)
	end if
	
	return tValue
end handler

private handler setBrowserProperty(in pProperty as String, in pValue as any)
	variable tProperty as Integer
	if not lookupProperty(pProperty, tProperty) then
		return
	end if
	
	if pProperty is in kStringProps then
		/* UNCHECKED */
		MCBrowserSetStringProperty(mBrowser, tProperty, pValue)
	else if pProperty is in kBoolProps then
		/* UNCHECKED */
		MCBrowserSetBoolProperty(mBrowser, tProperty, pValue)
	end if
end handler

--

private handler loadProperties(in pProperties as Array)
	variable tKey
	repeat for each element tKey in kPersistentProps
		if tKey is among the keys of pProperties then
			setProperty(tKey, pProperties[tKey])
		end if
	end repeat
	if "url" is among the keys of pProperties then
		setUrl(pProperties["url"])
	else if "htmltext" is among the keys of pProperties then
		setHtmlText(pProperties["htmltext"])
	end if
end handler

private handler saveProperties() returns Array
	variable tProps
	put the empty array into tProps
	
	variable tKey
	repeat for each element tKey in kPersistentProps
		put getProperty(tKey) into tProps[tKey]
	end repeat
	
	if mContentSource is not nothing then
		put getProperty(mContentSource) into tProps[mContentSource]
	end if
	
	return tProps
end handler

end widget