/*
Copyright (C) 2015 Runtime Revolution Ltd.

This file is part of LiveCode.

LiveCode is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License v3 as published by the Free
Software Foundation.

LiveCode is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
for more details.

You should have received a copy of the GNU General Public License
along with LiveCode.  If not see <http://www.gnu.org/licenses/>.  */

/*
Module: com.livecode.extension.livecode.browser

Type: widget

Description: This widget is a web browser.
*/

-- declaring extension as widget, followed by identifier
widget com.livecode.extensions.livecode.browser
--

-- dependancy declarations
use com.livecode.foreign
use com.livecode.widget
use com.livecode.engine
--

-- adding metadata to ensure the extension displays correctly in livecode
metadata title is "Browser"
metadata author is "Ian Macphail"
metadata version is "1.0.0"
--

property uri get getUrl set setUrl
metadata uri.editor is "com.livecode.pi.text"

property htmltext get getHtmlText set setHtmlText
metadata htmltext.editor is "com.livecode.pi.text"

property scrollbars get getScrollbars set setScrollbars
metadata scrollbars.editor is "com.livecode.pi.boolean"

property useragent get getUserAgent set setUserAgent
metadata useragent.editor is "com.livecode.pi.text"

property javascripthandlers get getJavaScriptHandlers set setJavaScriptHandlers
metadata javascripthandlers.editor is "com.livecode.pi.text"

--------------------------------------------------------------------------------

type MCBrowserFactoryRef is Pointer
type MCBrowserRef is Pointer

foreign handler MCBrowserFactoryGet(in pFactoryId as NativeCString, out rFactory as MCBrowserFactoryRef) returns CBool binds to "<builtin>"
foreign handler MCBrowserFactoryCreateBrowser(in pFactory as MCBrowserFactoryRef, out rBrowser as MCBrowserRef) returns CBool binds to "<builtin>"

foreign handler MCBrowserRetain(in pBrowser as MCBrowserRef) returns MCBrowserRef binds to "<builtin>"
foreign handler MCBrowserRelease(in pBrowser as MCBrowserRef) returns nothing binds to "<builtin>"

foreign handler MCBrowserGetNativeLayer(in pBrowser as MCBrowserRef) returns Pointer binds to "<builtin>"

foreign handler MCBrowserGetBoolProperty(in pBrowser as MCBrowserRef, in pProperty as CInt, out rValue as CBool) returns CBool binds to "<builtin>"
foreign handler MCBrowserSetBoolProperty(in pBrowser as MCBrowserRef, in pProperty as CInt, in pValue as CBool) returns CBool binds to "<builtin>"

foreign handler MCBrowserGetStringProperty(in pBrowser as MCBrowserRef, in pProperty as CInt, out rValue as NativeCString) returns CBool binds to "<builtin>"
foreign handler MCBrowserSetStringProperty(in pBrowser as MCBrowserRef, in pProperty as CInt, in pValue as NativeCString) returns CBool binds to "<builtin>"

foreign handler MCBrowserGoBack(in pBrowser as MCBrowserRef) returns CBool binds to "<builtin>"
foreign handler MCBrowserGoForward(in pBrowser as MCBrowserRef) returns CBool binds to "<builtin>"
foreign handler MCBrowserGoToURL(in pBrowser as MCBrowserRef) returns CBool binds to "<builtin>"
foreign handler MCBrowserEvaluateJavaScript(in pBrowser as MCBrowserRef, in pScript as NativeCString, out rResult as NativeCString) returns CBool binds to "<builtin>"

// TODO - declare functions that accept callbacks (not sure of the format here)

--------------------------------------------------------------------------------

variable mBrowser as optional MCBrowserRef
variable mBrowserType as String
variable mProperties as optional Array

--

variable sPropertyMap as Array
variable sStringProps as List
variable sBoolProps as List

--------------------------------------------------------------------------------

public handler OnLoad()
	put the empty array into sPropertyMap
	put 0 into sPropertyMap["scrollbars"]
	put 1 into sPropertyMap["allowNewWindows"]
	put 2 into sPropertyMap["enableContextMenu"]
	put 3 into sPropertyMap["url"]
	put 4 into sPropertyMap["htmlText"]
	put 5 into sPropertyMap["userAgent"]
	put 6 into sPropertyMap["javaScriptHandlers"]
	
	put ["url", "htmlText", "userAgent", "javaScriptHandlers"] into sStringProps
	put ["scrollbars", "allowNewWindows", "enableContextMenu"] into sBoolProps
end handler

----------

-- called when the widget is created
public handler OnCreate()
	OnLoad() -- TODO - placed this here temporarily - need proper module init

	put the empty string into mBrowserType
	put the empty array into mProperties
end handler

--

public handler OnAttach()
	variable tParent as Pointer
	// TODO - add widget code to get stack window handler
	// put my native window into tParent

	variable tFactory as MCBrowserFactoryRef
	/* UNCHECKED */
	MCBrowserFactoryGet(mBrowserType, tFactory)
	
	/* UNCHECKED */
	MCBrowserFactoryCreateBrowser(tFactory, mBrowser)
	
	variable tBrowserView as Pointer
	/* UNCHECKED */
	put MCBrowserGetNativeLayer(mBrowser) into tBrowserView
	
	// TODO - add widget code to set native layer from view
	set my native layer to tBrowserView
	
	setBrowserProperties(mProperties)
end handler

--

public handler OnDetach()
	// Save browser properties
	put getBrowserProperties() into mProperties
	
	// TODO - add widget code to set native layer from view
	set my native layer to nothing
	
	MCBrowserRelease(mBrowser)
	put nothing into mBrowser -- TODO - check if this is equivalent to mBrowser = nil
end handler

----------

private handler getProperty(in pProperty as String) returns any
	if mBrowser is nothing then
		return mProperties[pProperty]
	else
		return getBrowserProperty(pProperty)
	end if
end handler

private handler setProperty(in pProperty as String, in pValue as any)
	if mBrowser is nothing then
		put pValue into mProperties[pProperty]
	else
		setBrowserProperty(pProperty, pValue)
	end if
end handler

--

public handler getUrl() returns String
	return getProperty("url")
end handler

public handler setUrl(in pUrl as String)
	setProperty("url", pUrl)
end handler

--

public handler getHtmlText() returns String
	return getProperty("htmlText")
end handler

public handler setHtmlText(in pHtmlText as String)
	setProperty("htmlText", pHtmlText)
end handler

--

public handler getUserAgent() returns String
	return getProperty("userAgent")
end handler

public handler setUserAgent(in pUserAgent as String)
	setProperty("userAgent", pUserAgent)
end handler

--

public handler getJavaScriptHandlers() returns String
	return getProperty("javaScriptHandlers")
end handler

public handler setJavaScriptHandlers(in pJavaScriptHandlers as String)
	setProperty("javaScriptHandlers", pJavaScriptHandlers)
end handler

--

public handler getScrollbars() returns Boolean
	return getProperty("scrollbars")
end handler

public handler setScrollbars(in pScrollbars as Boolean)
	setProperty("scrollbars", pScrollbars)
end handler

----------

private handler propertyEnum(in pProp as String) returns Integer
	return sPropertyMap[pProp]
end handler

----------

private handler getBrowserProperty(in pProperty as String) returns any
	variable tValue as optional any
	
	if pProperty is in sStringProps then
		/* UNCHECKED */
		MCBrowserGetStringProperty(mBrowser, propertyEnum(pProperty), tValue)
	else if pProperty is in sBoolProps then
		/* UNCHECKED */
		MCBrowserGetBoolProperty(mBrowser, propertyEnum(pProperty), tValue)
	end if
	
	return tValue
end handler

private handler setBrowserProperty(in pProperty as String, in pValue as any)
	if pProperty is in sStringProps then
		/* UNCHECKED */
		MCBrowserSetStringProperty(mBrowser, propertyEnum(pProperty), pValue)
	else if pProperty is in sBoolProps then
		/* UNCHECKED */
		MCBrowserSetBoolProperty(mBrowser, propertyEnum(pProperty), pValue)
	end if
end handler

--

private handler setBrowserProperties(in pProperties as Array)
	variable tKey
	repeat for each key tKey in pProperties
		if mProperties[tKey] is not nothing then
			setBrowserProperty(tKey, pProperties[tKey])
		end if
	end repeat
end handler

private handler getBrowserProperties() returns Array
	variable tProps
	put the empty array into tProps
	
	variable tKey
	repeat for each key tKey in sPropertyMap
		put getBrowserProperty(tKey) into tProps[tKey]
	end repeat
	
	return tProps
end handler

end widget