/*
Copyright (C) 2015 Runtime Revolution Ltd.

This file is part of LiveCode.

LiveCode is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License v3 as published by the Free
Software Foundation.

LiveCode is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
for more details.

You should have received a copy of the GNU General Public License
along with LiveCode.  If not see <http://www.gnu.org/licenses/>.  */

/*
Palette header bar widget.
*/

widget com.livecode.widget.paletteHeader

use com.livecode.canvas
use com.livecode.widget
use com.livecode.engine
use com.livecode.library.fontawesomeSVGPath

metadata title is "Palette Header"
metadata author is "LiveCode"
metadata version is "1.0.0"

property navNames		get getNavNames			set setNavNames
property navIcons		get getNavIcons			set setNavIcons
property navLabels		get getNavLabels		set setNavLabels
property showNavIcons	get mShowNavIcons		set setShowNavIcons
property navSelected	get mSelectedNavItem	set setSelectedNavItem

property navData		get getNavData			set setNavData

property actionNames		get getActionNames		set setActionNames
property actionIcons		get getActionIcons		set setActionIcons
property actionLabels		get getActionLabels		set setActionLabels
property showActionIcons	get mShowActionIcons	set setShowActionIcons

property actionData		get getActionData		set setActionData


private variable mNavData 			as List
private variable mSelectedNavItem 	as Integer
private variable mShowNavIcons			as Boolean

private variable mActionData		as List
private variable mShowActionIcons	as Boolean

private variable mSize 				as Real

private variable mTabRects			as List
private variable mActionRects		as List

private variable mRecalculate		as Boolean

constant kBlack is [0,0,0]
constant kWhite is [1,1,1]

constant kDefaultNavName is "Nav"
constant kDefaultNavIcon is ""
constant kDefaultNavLabel is "nav"

constant kDefaultActionName is "Action"
constant kDefaultActionIcon is "lock"
constant kDefaultActionLabel is "action"

constant kTabPadding is 10
constant kActionPadding is 10

constant kHeightRatio is 0.8

public handler OnCreate()
	
	variable tAction as Array
	put the empty array into tAction
	put kDefaultActionName into tAction["name"]
	put kDefaultActionLabel into tAction["label"]
	put kDefaultActionIcon into tAction["icon"]
	
	put [tAction] into mActionData
	
	variable tNav as Array
	put the empty array into tNav
	variable tCount
	variable tCountString as String
	
	put the empty list into mNavData
	repeat with tCount from 1 up to 3
		put kDefaultNavName into tNav["name"]
		put kDefaultNavLabel into tNav["label"]
		put kDefaultNavIcon into tNav["icon"]
		push tNav onto mNavData
	end repeat
	
	put 1 into mSelectedNavItem
	put 10 into mSize
	
	put false into mShowNavIcons
	put true into mShowActionIcons
	
	put true into mRecalculate
end handler

public handler OnPaint()
	
	if mRecalculate is true then
		recalculate()
	end if
	
	-- draw the background rectangle
	set the paint of this canvas to getPaint("background")
	fill getPath("background") on this canvas
	
	-- draw the line around the header bar
	set the paint of this canvas to getPaint("line")
	stroke getPath("line") on this canvas
	
	variable tCount as Integer
	repeat with tCount from 1 up to the number of elements in mNavData
		drawTab(tCount)
	end repeat
	
	repeat with tCount from 1 up to the number of elements in mActionData
		drawAction(tCount)
	end repeat

end handler

public handler OnClick(in pX, in pY, in pWhich, in pAmount)
	variable tPoint as Point
	put the click position into tPoint
	
	variable tRect as Rectangle
	variable tCount as Integer
	put 0 into tCount
	repeat for each element tRect in mTabRects
		add 1 to tCount
		if tPoint is within tRect then
			setSelectedNavItem(tCount)
			exit repeat
		end if
	end repeat
	
	put 0 into tCount
	repeat for each element tRect in mActionRects
		add 1 to tCount
		if tPoint is within tRect then
			variable tAction as Array
			put element tCount of mActionData into tAction
			log "action" && tAction["label"] && "clicked"
			exit repeat
		end if
	end repeat
end handler

public handler OnGeometryChanged()
	put true into mRecalculate
end handler

--------------------------------------------------------------------------------
--
--		Painting utilities
--
--------------------------------------------------------------------------------

private handler drawTab(in pIndex as Integer)
	variable tTab as Array
	put element pIndex of mNavData into tTab
		
	variable tRect as Rectangle
	put element pIndex of mTabRects into tRect
	
	if pIndex is mSelectedNavItem then
		variable tPath as Path
		put rectangle path of tRect into tPath
		set the paint of this canvas to getPaint("line")
		stroke tPath on this canvas
		
		set the paint of this canvas to getPaint("selected tab")
		close path on tPath
		fill tPath on this canvas
	end if
	
	if mShowNavIcons then
		paintIcon(fontawesomeSVGPathFromName(tTab["icon"]), tRect, false)
	else
		set the font of this canvas to getFont("label")
		set the paint of this canvas to getPaint("title")	
	
		fill text tTab["label"] at center of tRect on this canvas
	end if
end handler

private handler drawAction(in pIndex as Integer)
	variable tAction as Array
	put element pIndex of mActionData into tAction

	variable tRect as Rectangle
	put element pIndex of mActionRects into tRect
	
	if mShowActionIcons then
		paintIcon(fontawesomeSVGPathFromName(tAction["icon"]), tRect, false)
	else
		set the font of this canvas to getFont("label")
		set the paint of this canvas to getPaint("title")	
	
		fill text tAction["label"] at center of tRect on this canvas
	end if
end handler

private handler recalculate()
	calculateTabRects()
	calculateActionRects()
	
	put false into mRecalculate
end handler

private handler calculateTabRects()
	variable tBoundary as Real
	put kTabPadding into tBoundary
	
	variable tRects as List
	put the empty list into tRects

	variable tLeft as Real
	variable tRight as Real
	put kTabPadding into tLeft
	
	variable tTop as Real
	put (1 - kHeightRatio) * my height into tTop
	
	variable tBottom as Real
	put my height into tBottom
	
	variable tRect as Rectangle
	variable tElement as Array
	repeat for each element tElement in mNavData
		if mShowNavIcons is false then
			variable tTextRect as Rectangle
			set the font of this canvas to getFont("label")
			measure tElement["label"] on this canvas into tTextRect
			put the width of tTextRect + 2 * kTabPadding + tLeft into tRight
			put rectangle [tLeft, tTop, tRight, tBottom] into tRect
		else
			put tBottom / 3 + 2 * kTabPadding + tLeft into tRight
			put rectangle [tLeft, tBottom / 3, tRight, 2 * tBottom / 3] into tRect
		end if
		
		push tRect onto tRects
		put tRight into tLeft
	end repeat
	
	put tRects into mTabRects
end handler

private handler calculateActionRects()
	variable tBoundary as Real
	put kActionPadding into tBoundary
	
	variable tRects as List
	put the empty list into tRects

	variable tLeft as Real
	variable tRight as Real
	put my width - kActionPadding into tRight
	
	variable tTop as Real
	put (1 - kHeightRatio) * my height into tTop
	
	variable tBottom as Real
	put my height into tBottom
	
	variable tRect as Rectangle
	variable tElement as Array
	repeat for each element tElement in mActionData
		if mShowActionIcons is false then
			variable tTextRect as Rectangle
			set the font of this canvas to getFont("label")
			measure tElement["label"] on this canvas into tTextRect
			put tRight - (the width of tTextRect + 2 * kActionPadding) into tLeft	
			put rectangle [tLeft, tTop, tRight, tBottom] into tRect	
		else
			put tRight - (tBottom / 3 + 2 * kActionPadding) into tLeft	
			put rectangle [tLeft, tBottom / 3, tRight, 2 * tBottom / 3] into tRect	
		end if

		push tRect onto tRects
		put tLeft into tRight
	end repeat
	
	put tRects into mActionRects
end handler

private handler getPaint(in pObject as String) returns Paint

	if pObject is "background" then
		return solid paint with color [246/255, 246/255, 247/255]
	
	else if pObject is "line" then
		return solid paint with color [178/255, 178/255, 178/255]
		
	else if pObject is "title" then
		 return solid paint with color kBlack
		 
	else if pObject is "selected tab" then
		return solid paint with color kWhite
	end if
	
end handler

private handler getFont(in pType as String) returns Font
	
	variable tFont as String
	put the name of the font of this canvas into tFont
	
	if pType is "title" then 
		return font tFont with bold style at size mSize
	else if pType is "label" then
		return font tFont at size mSize
	else if pType is "icon" then
		return font "fontawesome" at size mSize
	end if

end handler

private handler getPath(in pObject as String) returns Path
	if pObject is "background" or pObject is "line" then
		return rectangle path of rectangle [0, 0, my width, my height]	
	end if
end handler

private handler getRectOfTab(in pObject as String) returns Rectangle

	variable tLabelRect as Rectangle 
	put my bounds into tLabelRect
	
	if pObject is "label" then
		return tLabelRect

	end if
end handler

--------------------------------------------------------------------------------
--
--		Data utilities
--
--------------------------------------------------------------------------------

private handler listToArray(in pList as List) returns Array
	variable tCount
	variable tArray
	put the empty array into tArray
	repeat with tCount from 1 up to the number of elements in pList
		put element tCount of pList into tArray[tCount]
	end repeat
	return tArray
end handler

private handler setData(in pArray as Array, in pKeys as List, out rList as List)
	variable tOrder as List
	put the keys of pArray into tOrder
	sort tOrder in ascending order
	
	variable tOrderKey
	variable tKey
	variable tElement as Array
	variable tList as List
	put the empty list into tList
	
	repeat for each element tOrderKey in tOrder
		put the empty array into tElement	
		repeat for each element tKey in pKeys
			put pArray[tOrderKey][tKey] into tElement[tKey]
		end repeat
		push tElement onto tList
	end repeat
	
	put tList into rList
	
	put true into mRecalculate
	
	redraw all	
end handler

private handler getDataElement(in pElementName as String, in pList as List) returns String
	variable tList
	put the empty list into tList
	
	variable tElement
	repeat for each element tElement in pList
		push tElement[pElementName] onto tList
	end repeat
	
	variable tElements
	combine tList with "," into tElements
	return tElements
end handler

private handler setDataElement(in pElementName as String, in pElements as String, in pDefaultArray as Array, inout xList as List)
	variable tElements
	split pElements by "," into tElements

	variable tCount
	put 0 into tCount
	
	variable tElement
	repeat for each element tElement in xList
		add 1 to tCount
		if tCount > the number of elements in tElements then
			put pDefaultArray[pElementName] into xList[tCount][pElementName]
		else
			put tElements[tCount] into xList[tCount][pElementName]
		end if
	end repeat
	
	// If we there are more of the given element than there are items, generate more
	add 1 to tCount
	repeat with tCount from tCount up to the number of elements in tElements
		push pDefaultArray onto xList
		put tElements[tCount] into xList[tCount][pElementName]
	end repeat
	
	put true into mRecalculate
	
	redraw all	
end handler

// Only the primary data element ("name" for both actions and nav) can *reduce* the number of elements in the data list.
private handler setPrimaryDataElement(in pElementName as String, in pElements as String, in pDefaultArray as Array, inout xList as List)
	log "setting primary"
	variable tElements
	split pElements by "," into tElements

	variable tCount
	put 0 into tCount
	
	variable tElement
	repeat for each element tElement in tElements
		add 1 to tCount
		if tCount > the number of elements in xList then
			push pDefaultArray onto xList
		end if
		
		put tElement into xList[tCount][pElementName]
	end repeat
	
	log tCount
	log the number of elements in xList
	// If we there are more items than there are primary elements, pop them off.
	add 1 to tCount
	repeat with tCount from tCount up to the number of elements in xList
		log "popping"
		pop xList
	end repeat
	
	put true into mRecalculate
	
	redraw all	
end handler

--------------------------------------------------------------------------------
--
--		Nav data handling
--
--------------------------------------------------------------------------------

private handler defaultNavArray() returns Array
	variable tArray as Array
	put the empty array into tArray
	put kDefaultNavName into tArray["name"]
	put kDefaultNavLabel into tArray["label"]
	put kDefaultNavIcon into tArray["icon"]
	return tArray
end handler

private handler getNavData() returns Array
	return listToArray(mNavData)
end handler

private handler setNavData(in pNavData as Array)
	setData(pNavData, the keys of defaultNavArray(), mNavData)
end handler

private handler getNavNames() returns String
	return getDataElement("name", mNavData)
end handler

private handler setNavNames(in pNames as String)
	setPrimaryDataElement("name", pNames, defaultNavArray(), mNavData)
end handler

private handler getNavIcons() returns String
	return getDataElement("icon", mNavData)
end handler

private handler setNavIcons(in pIcons as String)
	setDataElement("icon", pIcons, defaultNavArray(), mNavData)
end handler

private handler getNavLabels() returns String
	return getDataElement("label", mNavData)
end handler

private handler setNavLabels(in pLabels as String)
	setDataElement("label", pLabels, defaultNavArray(), mNavData)
end handler

private handler setShowNavIcons(in pShowIcons as Boolean)
	put pShowIcons into mShowNavIcons
	redraw all
end handler

private handler setSelectedNavItem(in pItem as Integer)
	put pItem into mSelectedNavItem
	redraw all
end handler

--------------------------------------------------------------------------------
--
--		Action data handling
--
--------------------------------------------------------------------------------

private handler defaultActionArray() returns Array
	variable tArray as Array
	put the empty array into tArray
	put kDefaultActionName into tArray["name"]
	put kDefaultActionLabel into tArray["label"]
	put kDefaultActionIcon into tArray["icon"]
	return tArray
end handler

private handler getActionData() returns Array
	return listToArray(mActionData)
end handler

private handler setActionData(in pActions as Array)
	setData(pActions, the keys of defaultActionArray(), mActionData)
end handler

public handler setShowActionIcons(in pShowIcons as Boolean)
	put pShowIcons into mShowActionIcons
	redraw all
end handler

private handler getActionNames() returns String
	return getDataElement("name", mActionData)
end handler

private handler setActionNames(in pNames as String)
	setPrimaryDataElement("name", pNames, defaultActionArray(), mActionData)
end handler

private handler getActionIcons() returns String
	return getDataElement("icon", mActionData)
end handler

private handler setActionIcons(in pIcons as String)
	setDataElement("icon", pIcons, defaultActionArray(), mActionData)
end handler

private handler getActionLabels() returns String
	return getDataElement("label", mActionData)
end handler

private handler setActionLabels(in pLabels as String)
	setDataElement("label", pLabels, defaultActionArray(), mActionData)
end handler

--------------------------------------------------------------------------------
--
--		The following code is essentially duplicated from the svgpath widget.
--		Until we have widget 'components', this code will have to be here
--
--------------------------------------------------------------------------------

private handler measureIcon(in pPath as String) returns Rectangle
	variable tPath as Path
	put path pPath into tPath
	return the bounding box of tPath
end handler

private handler paintIcon(in pPath as String, in pTargetRect as Rectangle, in pClicked as Boolean)
	variable tPath as Path
	put path pPath into tPath
	
	variable tBounds
	put the bounding box of tPath into tBounds
	
	// set origin of bounds to 0,0
	translate tPath by [- the x of tBounds, - the y of tBounds]
	// flip vertically
	scale tPath by [1,-1]
	translate tPath by [0, the height of tBounds]
		
	// scale to fit within widget and maintain aspect ratio

	// Prepare values for matrix transformation
	variable isLarger as Boolean
	variable sx as Number
	variable sy as Number
		
	put false into isLarger
	put the width of pTargetRect / the width of tBounds into sx
	put the height of pTargetRect / the height of tBounds into sy
		
	if sx > sy then
		put true into isLarger
		put sy into sx
	else
		put sx into sy
	end if
		
	variable tx as Number
	variable ty as Number
		
	put the left of pTargetRect - (the left of tBounds*sx) into tx
	put the top of pTargetRect - (the top of tBounds*sy) into ty
		
	variable diff as Number
		
	if isLarger then
		put the width of pTargetRect - (the width of tBounds*sy) into diff
	else
		put the height of pTargetRect - (the height of tBounds*sy) into diff
	end if	
			
	// align center
	divide diff by 2
		
	if isLarger then
		add diff to tx
	else
		add diff to ty
	end if
		
	// create transformation matrix and apply
	variable tTransform as Transform
	put transform with matrix [sx, 0, 0, sy, tx, ty] into tTransform
	transform tPath by tTransform

	set the paint of this canvas to solid paint with color kBlack
	fill tPath on this canvas
end handler

end widget
