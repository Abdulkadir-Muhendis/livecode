module com.livecode.array

public foreign handler MCArrayEvalKeysOf(in Target as array, out Value as list) as undefined binds to "<builtin>"
public foreign handler MCArrayEvalElementsOf(in Target as array, out Value as list) as undefined binds to "<builtin>"

public foreign handler MCArrayEvalIsAmongTheKeysOfCaseless(in Needle as string, in IsNot as bool, in Target as array, out Result as bool) as undefined binds to "<builtin>"

public foreign handler MCArrayEvalIsAmongTheElementsOf(in Needle as any, in IsNot as bool, in Target as array, out Result as bool) as undefined binds to "<builtin>"

public foreign handler MCArrayEvalNumberOfElementsIn(in Target as array, out Count as index) as undefined binds to "<builtin>"

// Case insensitive map access / storage
public foreign handler MCArrayFetchElementOfCaseless(in Target as array, in Key as string, out Value as any) as undefined binds to "<builtin>"
public foreign handler MCArrayStoreElementOfCaseless(in Value as any, inout Target as array, in Key as string) as undefined binds to "<builtin>"

public foreign handler MCArrayEvalEmpty(out Value as array) as undefined binds to "<builtin>"

public foreign handler MCArrayRepeatForEachElement(inout Iterator as optional pointer, out Iterand as any, in Container as array) as bool binds to "<builtin>"
public foreign handler MCArrayRepeatForEachKey(inout Iterator as optional pointer, out Iterand as string, in Container as array) as bool binds to "<builtin>"

--

/* 
Summary:        Returns the keys of an array.
Target:         An expression which evaluates to an array.
output:         A list whose elements are the keys of <Target>. 

Example:
	variable tArray as array
	put the empty array into tArray
	put "value1" into tArray["key1"]
	put "value2" into tArray["key2"]
	put "value3" into tArray["key3"]
	
	variable tKeys as list
	put the keys of tArray into tKeys
	sort tKeys in ascending order
	
	variable tKeysString as string
	combine tKeys with "," into tKeysString 
	// tKeysString is "key1,key2,key3"

References:com.livecode.sort

Description:
>*Note:* The resulting list is not necessarily ordered in any way. Use the <com.livecode.sort> library to sort the keys.  
*/

syntax KeysOf is prefix operator with precedence 2
    "the" "keys" "of" <Target: Expression>
begin
    MCArrayEvalKeysOf(Target, output)
end syntax

/* 
Summary:        Returns the elements of an array.
Target:         An expression which evaluates to an array.
output:         A list whose elements are the elements of <Target>. 

Example:
	variable tArray as array
	put the empty array into tArray
	put 1 into tArray["key1"]
	put 2 into tArray["key2"]
	put 3 into tArray["key3"]
	
	variable tElements as list
	put the elements of tArray into tElements
	sort tElements in ascending numeric order
	
	// tElements is [1, 2, 3]
	
References:com.livecode.sort

Description:
>*Note:* The resulting list is not necessarily ordered in any way. Use the <com.livecode.sort> library to sort the elements.
*/

syntax ElementsOf is prefix operator with precedence 2
    "the" "elements" "of" <Target: Expression>
begin
    MCArrayEvalElementsOf(Target, output)
end syntax

--

/*
Summary:        Returns the number of elements in <Target>
Target:         An expression which evaluates to an array.

Example:
	variable tArray as array
	put the empty array into tArray
	put 1 into tArray["key1"]
	put 2 into tArray["key2"]
	put 3 into tArray["key3"]
	
	variable tVar as integer
	put the number of elements in tArray into tVar -- tVar contains 3
*/

syntax CountElementsOf is prefix operator with precedence 1
    "the" "number" "of" "elements" "in" <Target: Expression>
begin
    MCArrayEvalNumberOfElementsIn(Target, output)
end syntax

--

/* 
Summary:        Determines if an array has a given key
Needle:         An expression which evaluates to an integer, string, or list of integers.
Target:         An expression which evaluates to an array.
output:         Returns true if <Needle> can be found among the keys of <Target>.

*/
                
syntax AmongKeysOf is neutral binary operator with precedence 1
    <Needle: Expression> ( "is not" <IsNot=true> | "is" <IsNot=false> | <IsNot=false> ) "among" "the" "keys" "of" <Target: Expression>
begin
//  EvalIsAmongTheKeysOfCaseSensitive(Needle, IsNot, Target, output)
    MCArrayEvalIsAmongTheKeysOfCaseless(Needle, IsNot, Target, output)
    //MCArrayEvalIsAmongTheKeysOfNumeric(Needle, IsNot, Target, output)
    //MCArrayEvalIsAmongTheKeysOfMatrix(Needle, IsNot, Target, output)
end syntax

/* 
Summary:        Determines if an array contains a given element
Needle:         Any expression.
Target:         An expression which evaluates to an array.
output:         Returns true if <Needle> can be found among the elements of <Target>.

*/
                
syntax AmongElementsOf is neutral binary operator with precedence 1
    <Needle: Expression> ( "is not" <IsNot=true> | "is" <IsNot=false> | <IsNot=false> ) "among" "the" "elements" "of" <Target: Expression>
begin
    MCArrayEvalIsAmongTheElementsOf(Needle, IsNot, Target, output)
end syntax

--

/*

Summary:            Designates the element with key <Key> in <Target>.
Index: 				An expression which evaluates to a string.
Target:				An expression which evaluates to an array.

Example:			
	variable tArray as array
	put the empty array into tArray
	put "value" into tArray["key"]
	
	variable tVar as string
	put tArray["key"] into tVar -- tVar contains "value"

Description:     
Either locates the element container with the given key for use as the target container of another operation, or evaluates the element with the given key as the source of another operation.
*/

syntax SingletonElementOf is postfix operator with precedence 1
	<Target: Expression> "[" <Key: Expression> "]"
begin
    MCArrayFetchElementOfCaseless(Target, Key, output)
	MCArrayStoreElementOfCaseless(input, Target, Key)
end syntax

--

/* 

Summary: 		Designates the array with zero elements

Example:
	variable tVar as array
	variable tCount as int
	put the empty array into tVar
	put the number of elements in tVar into tCount -- tCount is 0
	
Description:
Use ```the empty array``` to initialise an array variable.

*/

syntax EmptyArray is expression
	"the" "empty" "array"
begin
	MCArrayEvalEmpty(output)
end syntax

--

/*
Summary:        Repeat over the elements of an array.
Iterand:        An expression of the form 'tVar in tArray'

Example:
    variable tArray as array
    put the empty array into tArray
    put 1 into tArray["key1"]
    put 2 into tArray["key2"]
    put 3 into tArray["key3"]

	variable tSum as number
	put 0 into tSum
    
    variable tElement
    repeat for each element tElement in tArray
        add tElement to tSum
    end repeat

    // tSum is 6
*/

syntax RepeatForEachElement is iterator
    "element" <Iterand: Expression>
begin
    MCArrayRepeatForEachElement(iterator, Iterand, container)
end syntax

/*
Summary:        Repeat over the keys of an array.
Iterand:        An expression of the form 'tVar in tArray'

Example:
    variable tArray as array
    put the empty array into tArray
    put 1 into tArray["abc"]
    put 2 into tArray["def"]
    put 3 into tArray["ghi"]

	variable tString as string
	put "" into tString
    
    variable tKey as string
    repeat for each key tKey in tArray
        put tKey after tString
    end repeat

    variable tBool as bool
    put tString contains "abc" into tBool -- tBool is true
*/

syntax RepeatForEachKey is iterator
    "key" <Iterand: Expression>
begin
    MCArrayRepeatForEachKey(iterator, Iterand, container)
end syntax

end module