/*  
This module specifies the syntax definitions and bindings for file i/o operations in modular LiveCode.
*/

module com.livecode.file

public foreign type Stream binds to "kMCStreamTypeInfo"

public foreign handler MCFileExecOpenFileForRead(in File as string) as optional Stream binds to "<builtin>"
public foreign handler MCFileExecOpenFileForWrite(in Create as bool, in File as string) as optional Stream binds to "<builtin>"
public foreign handler MCFileExecOpenFileForUpdate(in Create as bool, in File as string) as optional Stream binds to "<builtin>"

public foreign handler MCFileExecReadFromStream(in Amount as uint, in Source as Stream) as data binds to "<builtin>"

--

syntax OpenFileForRead is statement
    "open" "read" "only" "stream" "for" "file" <File: Expression>
begin
    MCFileExecOpenFileForRead(File)
end syntax

syntax OpenFileForWrite is statement
    "open" "write" "only" "stream" "for" ("new" <Create=true> | <Create=false>) "file" <File: Expression>
begin
    MCFileExecOpenFileForWrite(Create, File)
end syntax

syntax OpenFileForUpdate is statement
    "open" "read" "write" "stream" "for" ("new" <Create=true> | <Create=false>) "file" <File: Expression>
begin
    MCFileExecOpenFileForUpdate(Create, File)
end syntax

--

syntax ReadFromStream is statement
    "read" <Amount: Expression> "from" "stream" <Source: Expression>
begin
    MCFileExecReadFromStream(Amount, Source)
end syntax

end module